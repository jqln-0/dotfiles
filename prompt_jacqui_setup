#
# Jacqui's terminal prompt! This is essentially a port of oh-my-zsh's
# terminalparty theme to prezto.
#

pmodload 'helper'

function prompt_jacqui_precmd {
  # Degrade gracefully if for whatever reason the git module isn't around.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_jacqui_setup {
  # If these aren't added then you get a literal '$git_info[rprompt]' where
  # you'd expect the git info to be. No clue why, idk wtf I'm doing.
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Set up the precmd to run before each prompt.
  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_jacqui_precmd

  # Set up the git info part of the prompt.
  # Verbose may slow down the prompt, but it's needed for dirty checks to work
  # properly.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%b'
  zstyle ':prezto:module:git:info:clean' format ''
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}âš¡%F{yellow}'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '' \
    'rprompt' '%F{yellow}(%b%C%D)%f '

  # Displays ' % ', either green or red if the last command succeeded or failed
  # respectively.
  PROMPT='%(?,%F{green},%F{red}) %% %f'
  # Displays dir1/dir2 (master) hostname where dir1/dir2 are the parent and
  # current directories respectively, 'master' is the current git branch and
  # hostname is the current hostname. Has lots of fancy colours!
  RPROMPT='%F{white}%2~%f $git_info[rprompt]%B%F{blue}%m%f%b'
}

prompt_jacqui_setup "$@"

